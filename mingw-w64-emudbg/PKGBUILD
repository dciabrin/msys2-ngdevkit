# Maintainer: Damien Ciabrini <damien.ciabrini@gmail.com>

_realname=emudbg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2+202111140851
pkgrel=1
pkgvernightly=nightly-202111140851
pkgdesc="Emulator-agnostic source-level debugging API (mingw-w64)"
arch=('x86_64')
url='https://github.com/dciabrin/emudbg'
license=('LGPL3')
makedepends=("autoconf"
             "automake"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('!strip' '!buildflags' 'staticlibs')
source=(https://github.com/dciabrin/${_realname}/archive/refs/tags/${pkgvernightly}.tar.gz)
sha256sums=('427d92558be569f9db1ab09bfffa52b6a86f6474d5d21c97226603d4f234fe83')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  pushd ../${_realname}-${pkgvernightly}
  autoreconf -iv
  popd
  ../${_realname}-${pkgvernightly}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}

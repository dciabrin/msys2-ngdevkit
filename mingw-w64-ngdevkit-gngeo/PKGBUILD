# Maintainer: Damien Ciabrini <damien.ciabrini@gmail.com>

_realname=gngeo
pkgbase=mingw-w64-ngdevkit-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-ngdevkit-${_realname}"
pkgver=0.8.1+202302191800
pkgrel=2
pkgvernightly=nightly-202302191800
pkgdesc="Portable Neo-Geo emulator customized for ngdevkit (mingw-w64)"
arch=('x86_64')
url='https://github.com/dciabrin/gngeo'
license=('custom')
makedepends=("autoconf"
             "autoconf-archive"
             "automake"
             "make"
             "${MINGW_PACKAGE_PREFIX}-emudbg"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-glew")
options=('!strip' '!buildflags' 'staticlibs')
source=(https://github.com/dciabrin/${_realname}/archive/refs/tags/${pkgvernightly}.tar.gz)
sha256sums=('19020744682c1d81d06034eb383236ea5bebc99db08ce79c99746b0a6b734383')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  pushd ../${_realname}-${pkgvernightly}
  autoreconf -iv
  popd
  ../${_realname}-${pkgvernightly}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --program-prefix=ngdevkit- \
    --enable-msys2 \
    --with-glew \
    CFLAGS="-Wno-implicit-function-declaration -DGNGEORC=\\\"ngdevkit-gngeorc\\\"" \
    GL_LIBS="-L${MINGW_PREFIX}/bin -lglew32 -lopengl32"
  MSYS2_ARG_CONV_EXCL="-DDATA_DIRECTORY=" make pkgdatadir=${MINGW_PREFIX}/share/ngdevkit-gngeo
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install pkgdatadir=${MINGW_PREFIX}/share/ngdevkit-gngeo
}

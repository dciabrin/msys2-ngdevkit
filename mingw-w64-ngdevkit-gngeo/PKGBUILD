# Maintainer: Damien Ciabrini <damien.ciabrini@gmail.com>

_realname=gngeo
pkgbase=mingw-w64-ngdevkit-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-ngdevkit-${_realname}"
pkgver=0.8.1+202103142159
pkgrel=1
pkgvernightly=nightly-202103142159
pkgdesc="Portable Neo-Geo emulator customized for ngdevkit (mingw-w64)"
arch=('x86_64')
url='https://github.com/dciabrin/gngeo'
license=('custom')
makedepends=("autoconf"
             "autoconf-archive"
             "automake"
             "${MINGW_PACKAGE_PREFIX}-emudbg"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-glew")
options=('!strip' '!buildflags' 'staticlibs')
source=(https://github.com/dciabrin/${_realname}/archive/refs/tags/${pkgvernightly}.tar.gz)
sha256sums=('29869d0a288f0dae9fe86adcca8022b7d5b1862f5a3f13db1f23c3aaa6512199')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  pushd ../${_realname}-${pkgvernightly}
  autoreconf -iv
  popd
  ../${_realname}-${pkgvernightly}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --program-prefix=ngdevkit- \
    CFLAGS="-Wno-implicit-function-declaration -DGNGEORC=\\\"ngdevkit-gngeorc\\\"" \
    GL_LIBS="-L/bin -lglew32 -lopengl32"
  make -j1 pkgdatadir=${MINGW_PREFIX}/share/ngdevkit-gngeo
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install pkgdatadir=${MINGW_PREFIX}/share/ngdevkit-gngeo
}

# Maintainer: Damien Ciabrini <damien.ciabrini@gmail.com>

_realname=ngdevkit-toolchain
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1+202309050859
pkgrel=1
pkgvernightly=nightly-202309050859
pkgdesc="Toolchain for ngdevkit (mingw-w64)"
arch=('x86_64')
url='https://github.com/dciabrin/ngdevkit-toolchain'
license=('LGPL3')
makedepends=("autoconf"
             "automake"
             "bison"
             "gawk"
             "make"
             "patch"
             "sed"
             "tar"
             "texinfo"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-bzip2"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-readline"
             "${MINGW_PACKAGE_PREFIX}-xz")
depends=("flex"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-mpc"
         "${MINGW_PACKAGE_PREFIX}-mpfr"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('!strip' '!buildflags' 'staticlibs')
source=(https://github.com/dciabrin/${_realname}/archive/refs/tags/${pkgvernightly}.tar.gz)
sha256sums=('ad4e3aa6b5c5ba8adfc0e090fe1e97cc3d3ad449922320652515aae2340d9bcf')

pkgextraflags="--build=x86_64-w64-mingw32 --host=x86_64-w64-mingw32"

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  # building the toolchain only works with MSYS sed
  mkdir sed-msys2
  cp /usr/bin/sed.exe sed-msys2
  export PATH=$PWD/sed-msys2:$PATH
  # TODO: check why concurrent build segfaults on MSYS2
  make -C../${_realname}-${pkgvernightly} all BUILD=$(PWD)/build DESTDIR="${pkgdir}" prefix="${MINGW_PREFIX}" EXTRA_BUILD_FLAGS="${pkgextraflags}"
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  export PATH=$PWD/sed-msys2:$PATH
  make -C../${_realname}-${pkgvernightly} install BUILD=$(PWD)/build DESTDIR="${pkgdir}" prefix="${MINGW_PREFIX}" EXTRA_BUILD_FLAGS="${pkgextraflags}"
  # Remove the problematic symlinks
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  for i in ar ranlib; do
    rm -f ${pkgdir}${MINGW_PREFIX}/bin/m68k-neogeo-elf-$i.exe
    cat >${pkgdir}${MINGW_PREFIX}/bin/m68k-neogeo-elf-$i.exe <<EOF
#!/bin/sh
# NOTE: MINGW may be configured to implement symlinks with direct copies,
# which would break $i's plugin path. This wrapper execs into the real
# binary, which is enough to fix that problem.
exec ${MINGW_PREFIX}/m68k-neogeo-elf/bin/$i.exe \$*
EOF
    chmod +x ${pkgdir}${MINGW_PREFIX}/bin/m68k-neogeo-elf-$i.exe
  done
}
